pluginManagement {
    repositories {
        gradlePluginPortal()
    }
}

rootProject.name = 'opensearch-manager'

dependencyResolutionManagement {
    versionCatalogs {
        libs {
            from("ai.pipestream:pipestream-bom-catalog:0.7.8")
        }
    }

    repositories {
        // Maven Local for local development (releases only for ai.pipestream)
        mavenLocal {
            mavenContent {
                releasesOnly()
            }
            content {
                includeGroupByRegex "ai\\.pipestream(\\..*)?"
            }
        }

        mavenCentral()

        // Sonatype snapshots
        maven {
            url = uri('https://central.sonatype.com/repository/maven-snapshots/')
            mavenContent {
                snapshotsOnly()
            }
        }
        
        // GitHub Packages for ai.pipestream catalog and dependencies
        def githubToken = providers.environmentVariable("GITHUB_TOKEN")
            .orElse(providers.gradleProperty("gpr.key"))
            .orElse(providers.environmentVariable("GPR_TOKEN"))
        
        def githubActor = providers.environmentVariable("GITHUB_ACTOR")
            .orElse(providers.gradleProperty("gpr.user"))
            .orElse(providers.environmentVariable("GPR_USER"))
        
        if (githubToken.isPresent() && githubActor.isPresent()) {
            maven {
                url = uri("https://maven.pkg.github.com/ai-pipestream/bom")
                credentials {
                    username = githubActor.get()
                    password = githubToken.get()
                }
                content {
                    includeGroupByRegex "ai\\.pipestream(\\..*)?"
                }
            }
        }

        if (System.getenv('CI_ENV') != 'internal') {
            maven {
                url = uri('https://repository.apache.org/content/repositories/snapshots/')
                allowInsecureProtocol = false
                content {
                    includeGroupByRegex "org\\.apache\\.tika(\\..*)?"
                }
            }
        }
    }
}
