plugins {
    alias(libs.plugins.java)
    alias(libs.plugins.quarkus)
    alias(libs.plugins.maven.publish)
    id 'signing'
    id 'com.gradleup.nmcp' version '1.2.0'
}

def pipelineBomVersion = findProperty('pipelineBomVersion') ?: '0.1.2-SNAPSHOT'

dependencies {
    implementation 'io.quarkus:quarkus-container-image-docker'
    implementation platform("ai.pipestream:pipeline-bom:${pipelineBomVersion}")

    // Core Quarkus dependencies
    implementation libs.quarkus.arc
    implementation libs.quarkus.rest
    implementation libs.quarkus.rest.jackson
    implementation libs.quarkus.grpc
    implementation 'io.quarkus:quarkus-smallrye-health'
    implementation 'io.quarkus:quarkus-smallrye-stork'
    implementation 'io.quarkus:quarkus-messaging-kafka'
    implementation libs.apicurio.registry.protobuf.serde

    // Service discovery and registration with Consul
    implementation 'io.smallrye.stork:stork-service-discovery-consul'
    implementation 'io.smallrye.stork:stork-service-registration-consul'
    implementation 'io.smallrye.reactive:smallrye-mutiny-vertx-consul-client'

    // Pipestream platform libraries (managed via BOM/catalog)
    implementation libs.pipestream.compose.devservices
    implementation libs.pipestream.grpc.stubs
    implementation libs.pipestream.commons
    implementation libs.pipestream.dynamic.grpc
    implementation libs.pipestream.dynamic.grpc.registration.clients

    // OpenSearch client and supporting utilities
    implementation libs.opensearch.java
    implementation libs.httpclient5
    implementation libs.commons.lang3

    // Testing
    testImplementation libs.quarkus.junit5
    testImplementation libs.rest.assured
    testImplementation libs.pipestream.grpc.wiremock
}

group = 'ai.pipestream'
version = pipelineBomVersion

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

test {
    systemProperty "java.util.logging.manager", "org.jboss.logmanager.LogManager"
}
compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-parameters'
}

compileTestJava {
    options.encoding = 'UTF-8'
}

// Publishing configuration
publishing {
    publications {
        maven(MavenPublication) { publication ->
            from components.java
            pom {
                name.set('OpenSearch Manager')
                description.set('OpenSearch indexing and schema management service for the Pipestream platform')
                url.set('https://github.com/ai-pipestream/opensearch-manager')

                licenses {
                    license {
                        name.set('MIT License')
                        url.set('https://opensource.org/license/mit')
                    }
                }

                developers {
                    developer {
                        id.set('krickert')
                        name.set('Pipestream Platform Team')
                    }
                }

                scm {
                    connection.set('scm:git:git://github.com/ai-pipestream/opensearch-manager.git')
                    developerConnection.set('scm:git:ssh://github.com/ai-pipestream/opensearch-manager.git')
                    url.set('https://github.com/ai-pipestream/opensearch-manager')
                }
            }
        }
    }
    repositories {
        // Publish to Maven Local for development
        mavenLocal()
        def githubToken = providers.environmentVariable("GITHUB_TOKEN")
        if (githubToken.isPresent()) {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/ai-pipestream/opensearch-manager")
                credentials {
                    username = providers.environmentVariable("GITHUB_ACTOR").getOrElse("github-actions")
                    password = githubToken.get()
                }
                mavenContent {
                    releasesOnly()
                }
            }
        }
    }
}

signing {
    def signingKey = System.getenv("GPG_SIGNING_KEY")
    def signingPassword = System.getenv("GPG_SIGNING_PASSPHRASE")

    if (signingKey && signingPassword) {
        useInMemoryPgpKeys(signingKey, signingPassword)
        sign publishing.publications.maven
    }
}

nmcp {
    publishAllPublicationsToCentralPortal {
        username = providers.environmentVariable("MAVEN_CENTRAL_USERNAME")
        password = providers.environmentVariable("MAVEN_CENTRAL_PASSWORD")
        publishingType = "AUTOMATIC"
    }
}
